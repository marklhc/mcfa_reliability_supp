TITLE:  Saturated model at level 1 and 1-factor model at level 2 (to obtain 
        omega^b);
DATA:   FILE = timss_usa.dat;
VARIABLE: 
        ! Last item has been reversely coded
        NAMES = IDSCHOOL AS4MAMOR AS4MAENJ AS4MALIK AS4MABOR;
        USEVAR = AS4MAMOR-AS4MABOR;
        CLUSTER = IDSCHOOL;
        MISSING = ALL(-99);
ANALYSIS:
        TYPE = TWOLEVEL;
        ESTIMATOR = BAYES;
        BITERATIONS = 20000 (5000);  ! minimum 5,000 iterations
        BCONVERGENCE = .025;
        PROCESS = 2;
MODEL:  %WITHIN%
        AS4MAMOR WITH AS4MAENJ-AS4MABOR (covw12-covw14);
        AS4MAENJ WITH AS4MALIK-AS4MABOR (covw23-covw24);
        AS4MALIK WITH AS4MABOR (covw34);
        AS4MAMOR-AS4MABOR (varw1-varw4);
        %BETWEEN%
        f1b BY AS4MAMOR-AS4MABOR* (l1-l4);
        f1b@1;
        AS4MAMOR-AS4MABOR* (thb1-thb4);
MODEL CONSTRAINT:
        NEW(omegab);
        omegab = (l1 + l2 + l3 + l4)^2 * 1 /
                 ((l1 + l2 + l3 + l4)^2 * 1 + thb1 + thb2 + thb3 + thb4 + 
                 (varw1 + varw2 + varw3 + varw4 + 
                  2 * (covw12 + covw13 + covw14 + covw23 + covw24 + covw34)) / 
                  25.1);
OUTPUT: 
